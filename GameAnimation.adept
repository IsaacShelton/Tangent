
import 'addons.adept'

struct GameAnimation (frames *CaptTexture, length, current_frame, ticker, interval usize) {
    func destroy {
        each CaptTexture in [this.frames, this.length], it.free()
        delete this.frames
    }
    
    func reset {
        this.current_frame = 0
        this.ticker = 0
    }
    
    func now CaptTexture
        = this.frames[this.current_frame]
    
    func update {
        if ++this.ticker > this.interval {
            this.ticker = 0
            this.advance()
        }
    }
    
    func advance {
        if ++this.current_frame == this.length, this.current_frame = 0
    }
    
    func newInstance GameAnimation {
        anim GameAnimation = *this
        anim.current_frame = 0
        anim.ticker = 0
        return anim
    }
}

func gameAnimation(pattern String, beginning, end, ticker_interval int) GameAnimation {
    anim GameAnimation
    anim.interval = ticker_interval
    
    if beginning <= end {
        anim.length = end - beginning + 1
        anim.frames = new CaptTexture * anim.length
        
        n int = beginning; while n <= end {
            anim.frames[n - beginning] = captTexture(pattern % n, false)
            n += 1
        }
    } else {
        anim.length = beginning - end + 1
        anim.frames = new CaptTexture * anim.length
        
        n int = end; while n <= beginning {
            anim.frames[anim.length - n + end - 1] = captTexture(pattern % n, false)
            n += 1
        }
    }
    
    return anim
}

func gameAnimation(frames *CaptTexture, length usize, interval usize) GameAnimation {
    anim GameAnimation = undef
    anim.frames = frames
    anim.length = length
    anim.current_frame = 0
    anim.ticker = 0
    anim.interval = interval
    return anim
}
